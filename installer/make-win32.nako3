# ---------------------------------------------
# なでしこ3 for win32 生成スクリプト
# ---------------------------------------------
# 以下のようにディレクトリを配置
# <root>\nadesiko3 ← このファイルのルートフォルダ
# <root>\nadesiko3win32

コピー元＝母艦パスを「../」で相対パス展開
出力先＝母艦パスを「../../nadesiko3win32」で相対パス展開
テンプレート元＝母艦パスを「nako3-win32-template」で相対パス展開

「==========
出力先={出力先}
コピー元={コピー元}
テンプレート元={テンプレート元}
=========」を表示。

### テスト:ここから ###
もし、テンプレート元が存在するならば
  「OK:テンプレート存在」と表示
違えば
  「NG:準備が必要です：テンプレートがありません」のエラー発生
  終わり。
ここまで。

# --- nakopadがあるか？
#もし、「{テンプレート元}/bin/nakopad.exe」が存在するならば
#  「テンプレート正常:nakopad」と表示。
#違えば
#  「準備が必要です：nakopad.exeをテンプレートのbin/にコピーしてください。」のエラー発生
#  終わり。
#ここまで。

# --- Node.jsがあるか？
NODEJS=「{出力先}/nodejs.7z」
もし、NODEJSが存在するならば
  「OK:NODEJS存在」と表示。
違えば
  「NG:準備が必要です：node.jsのwin32(32bit)版を解凍して、『{出力先}/nodejs』に配置してください。最初にgit cloneでnadesiko3win32をダウンロードしてください。」のエラー発生
  終わり。
ここまで。
### テスト:ここまで ###

# メイン処理
コマンド一覧＝「#!/usr/bin/env cnako3
# ---ここから実行---
」
バッチ作成。
コマンド一覧を表示。
コマンド一覧を「{コピー元}/win32.nako3」へ保存。
「以上のバッチを以下のパスに保存しました。処理内容を確認した上で
.\{コピー元}/win32.nako3
と実行してください。」と表示。

●バッチ作成
  「README.md」を個別コピー処理。
  「package.json」を個別コピー処理。
  「webpack.config.mjs」を個別コピー処理。
  「bin」を削除付きファイルコピー処理。
  「batch」を削除付きファイルコピー処理。
  「demo」を削除付きファイルコピー処理。
  「doc」を削除付きファイルコピー処理。
  「release」を削除付きファイルコピー処理。
  「src」を削除付きファイルコピー処理。
  #「tmp」を削除付きファイルコピー処理。
  「test」を削除付きファイルコピー処理。
  「tools」を削除付きファイルコピー処理。
  「utils」を削除付きファイルコピー処理。
  「editor」を削除付きファイルコピー処理。
  テンプレートコピー処理。
  DS_STORE削除
  コマンド一覧＝コマンド一覧＆「『ok.』と表示。{改行}# --- ここまで ---」&改行。
ここまで

●DS_STORE削除
  コマンド一覧＝コマンド一覧＆「# --- 不要ファイルを列挙中 ---」&改行。
  出力先の全ファイル列挙。
  反復
    F=対象
    B=Fのファイル名抽出
    もし、B=「.DS_Store」ならば
      CMD=「"{F}"をファイル削除。」
      コマンド一覧＝コマンド一覧＆CMD&改行
    ここまで
  ここまで
ここまで

●テンプレートコピー処理
  # CMD = 「cp -f -r {テンプレート元}/* {出力先}/」
  CMD = 「"{テンプレート元}/"から"{出力先}/"へファイルコピー。」
　コマンド一覧＝コマンド一覧＆CMD&改行
ここまで

●(Pを)個別コピー処理とは
　FI＝「{コピー元}/{P}」
　FO=「{出力先}/{P}」
　# FIからFOへファイルコピー
　# CMD=「\cp -f {FI} {FO}」
　CMD=「"{FI}"から"{FO}"へファイルコピー。」
　コマンド一覧＝コマンド一覧＆CMD&改行
ここまで

●(PATHを)ファイルコピー処理とは
  # CMD=「cp -f {コピー元}/{PATH}/* {出力先}/{PATH}/」
  CMD=「『{コピー元}/{PATH}/*』から『{出力先}/{PATH}/』へファイルコピー。」
　コマンド一覧＝コマンド一覧&CMD&改行
ここまで

●(PATHを)削除付きファイルコピー処理とは
  # ---
  出力DIR=「{出力先}/{PATH}」
  もし、出力DIRのフォルダ存在するならば
    # CMD=「rm -f -r {出力DIR}」"
    CMD=「"{出力DIR}"をファイル削除。」
　　コマンド一覧＝コマンド一覧＆CMD&改行
  違えば
    出力DIRをフォルダ作成。
  ここまで。
  # ---
  # CMD=「cp -f -r {コピー元}/{PATH}/ {出力DIR}/」
　CMD=「"{コピー元}/{PATH}/"から"{出力DIR}/"へファイルコピー。」
　コマンド一覧＝コマンド一覧＆CMD&改行
ここまで
