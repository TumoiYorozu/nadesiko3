#
# command.txt を読んでWIKIの雛形を生成
#
プラグイン名＝空。
表題＝空。
母艦パス＝母艦パス取得
基本パス＝母艦パスからパス抽出
「{基本パス}/release/command.json」を開いてJSONデコードして、Jに代入。
目次＝「」
SQL=「」
TPL=「」
「{母艦パス}/command.txt」を開いて、改行で区切る。
ここから反復
　　トリム
   Sはそれ。
   ＃プラグイン名か？
　　Sを「/^■(.+)/」で正規表現マッチ。
　　もしそうならば
　　　　プラグイン名は、抽出文字列[0]
       目次＝目次＆改行＆「■ [[{プラグイン名}]]」&改行
　　　　続ける。
　　ここまで。
   ＃表題か？
　　Sを「/^●(.+)/」で正規表現マッチ。
　　もし、そうならば
　　　　表題は抽出文字列[0]
      # 目次＝目次＆「● [[{表題}:{プラグイン名}/{表題}]]」&改行
      目次＝目次＆改行&「● [[{表題}:{プラグイン名}]]」&改行
　　　　続ける。
　　ここまで。
   ＃コマンドか？
　　Sを「/^\|(.+)/」で正規表現マッチ。
　　もし、そうならば
　　　　CMDは抽出文字列[0]
　　　　CMDを「|」で区切る
　　　　C=それ
　　　　タイプ＝トリム(C[0])
　　　　名前＝トリム(C[1])
　　　　引数＝トリム(C[2])の「/」を「|」に置換
　　　　説明＝トリム(C[3])
　　　　ヨミ＝トリム(C[4])
       カナ1＝LEFT(ヨミ,1)
       もし、タイプ≠「関数」ならば
         目次＝目次＆「- [[{名前}:{プラグイン名}/{名前}]] --- {タイプ}」&改行
      違えば
        ARG＝引数
        もし、引数≠空ならば、ARG=「({引数})」
        目次＝目次＆「- [[{ARG}{名前}:{プラグイン名}/{名前}]] --- {説明}」&改行
      ここまで
      ＃関連語句の作成
      関連語句＝「」
      N=J@プラグイン名
      N=N@表題
      Nを反復
        NTYPE = それ@0
        NNAME = それ@1
        NDESC = それ@3
        もし、NTYPE≠「関数」ならば、NDESC=NTYPE
        関連語句＝関連語句＆「- [[{NNAME}:{プラグイン名}/{NNAME}]] --- {NDESC}」&改行
      ここまで
　　　　もし、タイプ＝「関数」ならば
　　　　　　関数雛形作成処理
　　　　違えば
          定数雛形作成処理
　　　　ここまで
　　ここまで。
ここまで。
「{母艦パス}/wiki-template.txt」にTPLを保存。
「{母艦パス}/wiki-template-sql.txt」にSQLを保存。
「{母艦パス}/wiki-index.txt」に(トリム(目次)&改行)を保存。

●関数雛形作成処理とは
  N=S{{{{{
■{名前} ({ヨミ})

<<<#csv(flag=||)
プラグイン名 || [[{プラグイン名}]]/{表題}
種類 || 関数
引数 || {引数}
説明 || {説明}
>>>

▲詳しい説明

なし

●利用例

なし

●関連する項目

{関連語句}
  }}}}}
  Nの「<<<」を『{{{』に置換
  「>>>」を『}}}』に置換
  「'」を「''」に置換
  トリム
  S=それ
  SQL=SQL&「INSERT INTO logs (name, body) VALUES ('{プラグイン名}/{名前}', '{S}');」&改行
  TPL=TPL&「------------------」&改行&S&改行
ここまで

●定数雛形作成処理とは
  N=S{{{{{
●{名前} ({ヨミ})

| カテゴリ | [[{プラグイン名}]]/[[{表題}:{プラグイン名}/{表題}]]
| 種類 | {タイプ}
| 初期値 | {説明}

▲詳しい説明

なし

●利用例

なし

●関連する項目

{関連語句}
  }}}}}
  Nの「'」を「''」に置換
  トリム
  S=それ
  SQL=SQL&「INSERT INTO logs (name, body) VALUES ('{プラグイン名}/{名前}', '{S}');」&改行
ここまで
